# 1 "/Users/home/paulinan/models/coupled_model_apptainer/examples/3comps/3comp_example.f90"
program atm_ocean_coupling
  use ESMF
  implicit none

!===========================
! Specification Section
!===========================
  type(ESMF_Grid) :: grid_atm, grid_ocn, grid_ice
  type(ESMF_FieldBundle) :: fb_atm, fb_ocn, fb_ice
  type(ESMF_State) :: state_atm, state_ocn, state_ice
  type(ESMF_VM) :: vm
  type(ESMF_RouteHandle) :: rh_atm2ocn, rh_ocn2atm, rh_ice2atm, rh_ice2ocn
  type(ESMF_Field) :: fld_atm_flux, fld_ocn_flux, fld_atm_sst, fld_ocn_sst, fld_ice_conc, fld_atm_conc, fld_ocn_conc
  integer :: rc
  integer, parameter :: nx_atm=64, ny_atm=32
  integer, parameter :: nx_ocn=128, ny_ocn=64
  integer, parameter :: nx_ice=128, ny_ice=64

  rc = 0
  
!===========================
! Executable Section
!===========================
  call ESMF_Initialize(logkindflag=ESMF_LOGKIND_MULTI, rc=rc)
!call ESMF_Initialize(rc=rc)
  if (rc /= ESMF_SUCCESS) stop "ESMF initialization failed"

  call ESMF_VMGetCurrent(vm, rc=rc)

!------------------------------------------------------------
! 1. Create Grids
  grid_atm = ESMF_GridCreate(maxIndex=(/nx_atm, ny_atm/), coordSys=ESMF_COORDSYS_CART, rc=rc)
  grid_ocn = ESMF_GridCreate(maxIndex=(/nx_ocn, ny_ocn/), coordSys=ESMF_COORDSYS_CART, rc=rc)
  grid_ice = ESMF_GridCreate(maxIndex=(/nx_ice, ny_ice/), coordSys=ESMF_COORDSYS_CART, rc=rc)

!------------------------------------------------------------
! 2. Create FieldBundles and States
  fb_atm = ESMF_FieldBundleCreate(name="AtmosphereBundle", rc=rc)
  fb_ocn = ESMF_FieldBundleCreate(name="OceanBundle", rc=rc)
  fb_ice = ESMF_FieldBundleCreate(name="IceBundle", rc=rc)

! Create fields and add to bundles
  fld_atm_flux = ESMF_FieldCreate(grid=grid_atm, typekind=ESMF_TYPEKIND_R8, name="heat_flux", rc=rc)
  fld_atm_sst  = ESMF_FieldCreate(grid=grid_atm, typekind=ESMF_TYPEKIND_R8, name="sst", rc=rc)
  fld_ocn_flux = ESMF_FieldCreate(grid=grid_ocn, typekind=ESMF_TYPEKIND_R8, name="heat_flux", rc=rc)
  fld_ocn_sst  = ESMF_FieldCreate(grid=grid_ocn, typekind=ESMF_TYPEKIND_R8, name="sst", rc=rc)
  fld_ice_conc  = ESMF_FieldCreate(grid=grid_ice, typekind=ESMF_TYPEKIND_R8, name="ice_concentration", rc=rc)
  fld_atm_conc  = ESMF_FieldCreate(grid=grid_atm, typekind=ESMF_TYPEKIND_R8, name="ice_concentration", rc=rc)
  fld_ocn_conc  = ESMF_FieldCreate(grid=grid_ocn, typekind=ESMF_TYPEKIND_R8, name="ice_concentration", rc=rc)

  call ESMF_FieldBundleAdd(fb_atm, (/fld_atm_flux, fld_atm_sst, fld_atm_conc/), rc=rc)
  call ESMF_FieldBundleAdd(fb_ocn, (/fld_ocn_flux, fld_ocn_sst, fld_ocn_conc/), rc=rc)
  call ESMF_FieldBundleAdd(fb_ice, (/fld_ice_conc/), rc=rc)

  state_atm = ESMF_StateCreate(name="AtmosphereState", rc=rc)
  state_ocn = ESMF_StateCreate(name="OceanState", rc=rc)
  state_ice = ESMF_StateCreate(name="IceState", rc=rc)

  call ESMF_StateAdd(state_atm, (/fb_atm/), rc=rc)
  call ESMF_StateAdd(state_ocn, (/fb_ocn/), rc=rc)
  call ESMF_StateAdd(state_ice, (/fb_ice/), rc=rc)

!------------------------------------------------------------
! 3. Compute the remapping weights
  call ESMF_FieldRegridStore(srcField=fld_atm_flux, dstField=fld_ocn_flux, routehandle=rh_atm2ocn, regridmethod=ESMF_REGRIDMETHOD_BILINEAR, rc=rc)
  call ESMF_FieldRegridStore(srcField=fld_ocn_sst, dstField=fld_atm_sst, routehandle=rh_ocn2atm, regridmethod=ESMF_REGRIDMETHOD_BILINEAR, rc=rc)
  call ESMF_FieldRegridStore(srcField=fld_ice_conc, dstField=fld_ocn_conc, routehandle=rh_ice2ocn, regridmethod=ESMF_REGRIDMETHOD_BILINEAR, rc=rc)
  call ESMF_FieldRegridStore(srcField=fld_ice_conc, dstField=fld_atm_conc, routehandle=rh_ice2atm, regridmethod=ESMF_REGRIDMETHOD_BILINEAR, rc=rc)

!------------------------------------------------------------
! 4. Initialize fields
  call initialize_fields(fld_atm_flux, fld_atm_sst, fld_ocn_flux, fld_ocn_sst, fld_ice_conc, fld_atm_conc, fld_ocn_conc)

!------------------------------------------------------------
! 5. Exchange Fluxes and Fields
  call ESMF_FieldRegrid(routehandle=rh_atm2ocn, srcField=fld_atm_flux, dstField=fld_ocn_flux, rc=rc)
  call ESMF_FieldRegrid(routehandle=rh_ocn2atm, srcField=fld_ocn_sst, dstField=fld_atm_sst, rc=rc)
  call ESMF_FieldRegrid(routehandle=rh_ice2ocn, srcField=fld_ice_conc, dstField=fld_ocn_conc, rc=rc)
  call ESMF_FieldRegrid(routehandle=rh_ice2atm, srcField=fld_ice_conc, dstField=fld_atm_conc, rc=rc)

!------------------------------------------------------------
! 6. Finalize
  call ESMF_Finalize(rc=rc)

contains

  subroutine initialize_fields(fld_atm_flux, fld_atm_sst, fld_ocn_flux, fld_ocn_sst, fld_ice_conc, fld_atm_conc, fld_ocn_conc)
    type(ESMF_Field), intent(inout) :: fld_atm_flux, fld_atm_sst, fld_ocn_flux, &
                                       fld_ocn_sst, fld_ice_conc, fld_atm_conc, &
                                       fld_ocn_conc
    real(8), pointer :: ptr(:,:)
    integer :: rc

    call ESMF_FieldGet(fld_atm_flux, farrayPtr=ptr, rc=rc)
    ptr = 100.0d0

    call ESMF_FieldGet(fld_atm_sst, farrayPtr=ptr, rc=rc)
    ptr = 290.0d0

    call ESMF_FieldGet(fld_ocn_flux, farrayPtr=ptr, rc=rc)
    ptr = 0.0d0

    call ESMF_FieldGet(fld_ocn_sst, farrayPtr=ptr, rc=rc)
    ptr = 300.0d0

    call ESMF_FieldGet(fld_ice_conc, farrayPtr=ptr, rc=rc)
    ptr = 0.5d0

    call ESMF_FieldGet(fld_atm_conc, farrayPtr=ptr, rc=rc)
    ptr = 0.6d0 

    call ESMF_FieldGet(fld_ocn_conc, farrayPtr=ptr, rc=rc)
    ptr = 0.4d0
  end subroutine initialize_fields

end program atm_ocean_coupling

